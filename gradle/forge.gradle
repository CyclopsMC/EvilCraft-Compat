repositories {
    mavenCentral()
    mavenLocal()
    maven {
        name "Cyclops Repo"
        url "https://oss.jfrog.org/artifactory/simple/libs-release/"
    }
    maven {
        name "Mobius Repo"
        url "http://mobiusstrip.eu/maven"
    }
    maven {
        name 'CB Maven FS'
        url "http://chickenbones.net/maven/"
    }
    maven {
        name 'DVS1 Maven FS'
        url 'http://dvs1.progwml6.com/files/maven'
    }
    maven {
        name 'Player Maven FS'
        url 'http://maven.ic2.player.to/'
    }
    maven {
        name = "CurseForge"
        url = "https://minecraft.curseforge.com/api/maven/"
    }
    maven {
        name "Tehnut Repo"
        url "http://tehnut.info/maven/"
    }
    maven {
        name "Blamejared Maven Repo"
        url "http://maven.blamejared.com/"
    }
    maven {
        name = "Terrag"
        url = "http://maven.tterrag.com/"
    }
}
dependencies {
    // Add something like 'evilcraft_version_local=0.1.0-DEV' to your gradle.properties if you want to use a custom local Integrated Dynamics version.
    if(project.hasProperty("evilcraft_version_local")) {
        compile "org.cyclops.evilcraft:EvilCraft:${config.minecraft_version}-${project.evilcraft_version_local}:deobf"
    } else {
        compile "org.cyclops.evilcraft:EvilCraft:${config.minecraft_version}-${config.evilcraft_version}:deobf"
    }
    // Add something like 'cyclopscore_version_local=0.1.0-DEV' to your gradle.properties if you want to use a custom local CyclopsCore version.
    if(project.hasProperty("cyclopscore_version_local")) {
        compile "org.cyclops.cyclopscore:CyclopsCore:${config.minecraft_version}-${project.cyclopscore_version_local}:deobf"
    } else {
        compile "org.cyclops.cyclopscore:CyclopsCore:${config.minecraft_version}-${config.cyclopscore_version}:deobf"
    }

    deobfCompile("org.cyclops.commoncapabilities:CommonCapabilities:${config.commoncapabilities_version}") { // https://dl.bintray.com/cyclopsmc/dev/org/cyclops/commoncapabilities/CommonCapabilities/
        exclude group: 'org.cyclops.cyclopscore', module: 'CyclopsCore'
    }

    deobfCompile "slimeknights.mantle:Mantle:${config.mantle_version}" // http://dvs1.progwml6.com/files/maven/slimeknights/mantle/Mantle/maven-metadata.xml
    deobfCompile "slimeknights:TConstruct:${config.tconstruct_version}" // http://dvs1.progwml6.com/files/maven/slimeknights/TConstruct/maven-metadata.xml
    deobfCompile "mcp.mobius.waila:Waila:${config.waila_version}" // http://mobiusstrip.eu/maven/mcp/mobius/waila/Waila/
    deobfCompile "net.sengir.forestry:forestry_${config.forestry_version}" // http://maven.ic2.player.to/
    //compile "com.pahimar.ee3:EquivalentExchange3:${config.minecraft_version}-${config.EE3_version}:dev" // http://files.minecraftforge.net/maven/com/pahimar/ee3/EquivalentExchange3
    deobfCompile "net.industrial-craft:industrialcraft-2:${config.IC2_version}" // http://maven.ic2.player.to/
    deobfCompile "info.amerifrance.guideapi:Guide-API:1.12-${config.guideapi_version}" // http://tehnut.info/maven/info/amerifrance/guideapi/Guide-API
    deobfCompile "com.wayoftime.bloodmagic:BloodMagic:1.12.2-${config.BM_version}" // http://tehnut.info/maven/com/wayoftime/bloodmagic/BloodMagic
    deobfCompile "mezz.jei:jei_${config.jei_version}" // https://dvs1.progwml6.com/jenkins/
    deobfCompile "blusunrize:ImmersiveEngineering:${config.ie_version}"// http://maven.blamejared.com/blusunrize/ImmersiveEngineering/
    //deobfCompile "com.enderio:EnderIO:1.9.4-${config.enderio_version}" // http://maven.tterrag.com/com/enderio/EnderIO
    deobfCompile "ender-io:EnderIO:${config.enderio_version}" // https://minecraft.curseforge.com/projects/ender-io/files
    //deobfCompile "com.enderio.core:EnderCore:1.9.4-${config.endercore_version}" // http://maven.tterrag.com/com/enderio/EnderIO
    deobfCompile "endercore:EnderCore:${config.endercore_version}" // https://minecraft.curseforge.com/projects/endercore/files
    deobfCompile ("CraftTweaker2:CraftTweaker2-API:${config.crafttweakerapi_version}") { // http://maven.blamejared.com/CraftTweaker2/CraftTweaker2-API/
        exclude group: 'org.ow2.asm', module: 'asm-debug-all'
    }

    // Only needed for testing
    deobfCompile ("CraftTweaker2:CraftTweaker2-MC1120-Main:${config.crafttweakermain_version}") { // http://maven.blamejared.com/CraftTweaker2/CraftTweaker2-MC1120-Main/
        exclude group: 'org.ow2.asm', module: 'asm-debug-all'
    }

    // Project lombok
    compile "org.projectlombok:lombok:1.14.4"
}


minecraft {
    version = "${config.minecraft_version}-${config.forge_version}"
    runDir = "run"
    
    replaceIn "Reference.java"
    replace "@VERSION@", project.version
    replace "@MC_VERSION@", project.config.minecraft_version
    replace "@FORGE_VERSION@", project.config.forge_version
    replace "@BUILD_NUMBER@", project.buildnumber
    replace "@CYCLOPSCORE_VERSION@", project.config.cyclopscore_version

    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "${config.mcp_mappings_version}"
}

if (project.buildnumber.equals("RELEASE"))
    version = "${config.minecraft_version}-${config.mod_version}"
else
    version = "${config.minecraft_version}-${config.mod_version}-${buildnumber}"

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, that is not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}
